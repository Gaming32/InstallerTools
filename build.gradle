plugins {
  id 'net.minecrell.licenser' version '0.3'
  id 'org.ajoberstar.grgit' version '2.3.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'

group = 'net.minecraftforge'
version = gitVersion()
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    maven { url = 'https://maven.minecraftforge.net/' }
    mavenCentral()
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name    
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/jarsplitter/**/*.java'
  newLine false
}

jar {
    manifest.attributes('Main-Class': 'net.minecraftforge.jarsplitter.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'net.minecraftforge.jarsplitter.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'net.minecraftforge:srgutils:0.4.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact shadowJar

            pom {
                name = 'Jar Splitter'
                description = 'Splits a jar file into classes and data, used in the Forge installer'
                url = 'https://github.com/MinecraftForge/JarSplitter'
                scm {
                    url = 'https://github.com/MinecraftForge/JarSplitter'
                    connection = 'scm:git:git://github.com/MinecraftForge/JarSplitter.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/JarSplitter.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/JarSplitter/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (System.env.MAVEN_USER) {
                url 'https://maven.minecraftforge.net/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER ?: 'not'
                    password = System.env.MAVEN_PASSWORD ?: 'set'
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
