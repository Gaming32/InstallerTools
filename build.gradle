plugins {
  id 'net.minecrell.licenser' version '0.3'
  id 'org.ajoberstar.grgit' version '2.3.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'

group = 'net.minecraftforge'
version = gitVersion()
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name    
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/installertools/**/*.java'
  newLine false
}

jar {
    manifest.attributes('Main-Class': 'net.minecraftforge.installertools.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'net.minecraftforge.installertools.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'net.md-5:SpecialSource:1.8.5'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact shadowJar

            pom {
                name = 'Installer Tools'
                description = 'A collection of command line tools that are useful for the Forge installer, that are not worth being their own standalone projects.'
                url = 'https://github.com/MinecraftForge/InstallerTools'
                scm {
                    url = 'https://github.com/MinecraftForge/InstallerTools'
                    connection = 'scm:git:git://github.com/MinecraftForge/InstallerTools.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/InstallerTools.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/InstallerTools/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('forgeMavenPassword')) {
                credentials {
                    username project.properties.forgeMavenUser
                    password project.properties.forgeMavenPassword
                }
                url 'http://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
